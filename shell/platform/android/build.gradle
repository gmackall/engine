// This file only exists to provide IDE support to Android Studio.
//
// See ./README.md for details.

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: "com.android.library"

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs './'
            java.include './io.flutter/**'
            test {
                java.srcDir './test'
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dependencies {
        implementation fileTree(include: ["*.jar"], dir: "../../../../third_party/android_embedding_dependencies/lib/")

        implementation "androidx.test:core:1.4.0"
        implementation "com.google.android.play:core:1.8.0"
        implementation "com.ibm.icu:icu4j:69.1"
        implementation "org.robolectric:robolectric:4.11"
        implementation "junit:junit:4.13.2"
        implementation "androidx.test.ext:junit:1.1.4-alpha07"

        def mockitoVersion = "4.7.0"
        implementation "org.mockito:mockito-core:$mockitoVersion"
        implementation "org.mockito:mockito-inline:$mockitoVersion"
        implementation "org.mockito:mockito-android:$mockitoVersion"
    }

    // Configure the embedding dependencies.
    apply from: new File(rootDir, '../../../tools/androidx/configure.gradle').absolutePath;
    configureDependencies(new File(rootDir, '../../..')) { dependency ->
        dependencies {
            implementation "$dependency"
        }
    }
}


